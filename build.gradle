
subprojects {
  apply plugin: 'groovy'

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }

  dependencies {
    compile 'org.slf4j:slf4j-api:1.7.22'
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    compile 'ch.qos.logback:logback-core:1.1.7',
      'ch.qos.logback:logback-classic:1.1.7'

    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
      exclude group: 'org.codehaus.groovy'
    }
    testCompile 'cglib:cglib-nodep:3.2.4',
      'org.objenesis:objenesis:2.4'
    testCompile 'au.com.dius:pact-jvm-consumer-groovy_2.12:3.5.14'
  }

  test {
    systemProperties['pact.rootDir'] = "$buildDir/pacts"
  }
}

task copyPactToDropwizard(type: Copy) {
  from "${project(':consumer').buildDir}/pacts"
  into "${project(':providers:dropwizard-provider').buildDir}/pacts"
}

task copyPactToSpringboot(type: Copy) {
  from "${project(':consumer').buildDir}/pacts"
  into "${project(':providers:springboot-provider').buildDir}/pacts"
}

task publishWorkshopPact(dependsOn: [copyPactToDropwizard, copyPactToSpringboot],
  description: 'Publish the pact file to the provider projects')
